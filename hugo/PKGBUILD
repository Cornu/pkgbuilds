#Maintainer: Hans-Harro Horn <h.h.horn@gmail.com>
pkgname=hugo
pkgver=0.13
pkgrel=1
pkgdesc="Fast and Flexible Static Site Generator in Go"
arch=('x86_64' 'i686' 'arm')
url="http://hugo.spf13.com/"
license=('custom:SimPL')
if [ "$CARCH" = "i686" ]; then
  _PKGARCH=386
elif [ "$CARCH" = "arm" ]; then
  _PKGARCH=arm
else
  _PKGARCH=amd64
fi
source_x86_64=("https://github.com/spf13/hugo/releases/download/v${pkgver}/${pkgname}_${pkgver}_linux_amd64.tar.gz")
source_i686=("https://github.com/spf13/hugo/releases/download/v${pkgver}/${pkgname}_${pkgver}_linux_386.tar.gz")
source_arm=("https://github.com/spf13/hugo/releases/download/v${pkgver}/${pkgname}_${pkgver}_linux_arm.tar.gz")
sha256sums_x86_64=('2c43f709a1e8c08ecae481cdf46e9b1d0ffb2ae36fcd8196e5ac32ec05bfb13b')
sha256sums_i686=('ba3ad0bffd7f8f48d9922f1db8446a3075c986d6887790152271fccb6447d6b2')
sha256sums_arm=('18aab494865adf9f2609cc49dccca962d8b76076185acef0bceaaf755c15383e')

package() {
  cd ${pkgname}_${pkgver}_linux_${_PKGARCH}

  install -Dm755 "${pkgname}_${pkgver}_linux_${_PKGARCH}" "${pkgdir}/usr/bin/${pkgname}"
  install -Dm644 LICENSE.md "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set ts=2 sw=2 et:

